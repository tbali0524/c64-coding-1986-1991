SYS36864
.OPT OO
;
; *********************************
;   BALI'S CHARACTER EDITOR  V2.0
; ASSEMBLY FORRASKOD (PROFI-ASS 64)
;  UTOLSO MODOSITAS 1991. APR. 1.
; (C)COPYRIGHT  TOTH BALINT (BALI)
;   1039 BUDAPEST, KABAR U.7.V.27.
;    HUNGARY  ( TEL. 1873-943 )
; *********************************
;
;
LINPRT  =$BDCD ; DECIMALIS KIIRAS
SCRTCH  =$A644 ; NEW
READY   =$A474 ; BASIC BELEPES
;
CINT    =$FF81
IOINIT  =$FF84
SETMSG  =$FF90
MEMBOT  =$FF9C
READST  =$FFB7
SETLFS  =$FFBA
SETNAM  =$FFBD
CHROUT  =$FFD2
LOAD    =$FFD5
SAVE    =$FFD8
STOP    =$FFE1
GETIN   =$FFE4
CLALL   =$FFE7
PLOT    =$FFF0
;
REGIIRQ =$EA31
ENDIRQ  =$FEBC
RESET   =$FCE2
;
SCRRAM  =$0400
COLRAM  =$D800
CHRRAM  =$2800
INVRAM  =$2C00
BASBOT  =$3000
CN      =2
TXTTAB  =43
NDX     =198
BLNSW   =204
CINV    =$0314
RASZTER =$82
HOMEPOZ =SCRRAM+71
SOR8POZ =HOMEPOZ+(7*40)
SETPOZ  =SCRRAM+(10*40)+4
SPACE   =32
INVSPACE =0
;
*=$0801
.WORD BASEND,1991
.BYTE $9E
.ASC "2080:"
.BYTE $8F,$22,20,20,20,20,20
.BYTE 20,20,20,20,20,20,20
.ASC "BALI"
.BYTE 0
BASEND .WORD 0
;
LDA #0
STA BASBOT
LDX #1
LDY #>BASBOT
STX TXTTAB
STY TXTTAB+1
CLC
JSR MEMBOT
JSR SCRTCH
;
JSR IOINIT
JSR CLALL
JSR CINT
LDA #%11000000
JSR SETMSG
LDA #11
STA $D021
LDA #0
STA $D022
LDA #6
STA $D023
LDA #0
STA $D024
LDA #7
STA $D027
LDA #0
STA CN
STA UNDOEN
UJKEP  JSR PRINTSCR
UJCHR  JSR PRINTCHR
RAJZOL JSR DATA
LOOP   JSR POZALL
CRSRON LDY #0
LDA #2
STA WINDPOZ
LDX #28
LDA ($FB),Y
PHA
BNE R085
LDX #44
R085 TXA
STA ($FB),Y
R011 JSR GETIN
CMP #0
BEQ R011
TAX
LDY #0
PLA
STA ($FB),Y
TXA
BILLELL LDX #BILLTABM
R012 DEX
BMI CRSRON
CMP BILLTAB,X
BNE R012
TXA
ASL A
TAX
LDA UGROTAB,X
STA TMP
LDA UGROTAB+1,X
STA TMP+1
CPX #41
BCC R054
LDA #>(RAJZOL-1)
PHA
LDA #<(RAJZOL-1)
PHA
R054 JMP (TMP)
;
*=$0900
.BYTE $FF,$FF,0
.BYTE $FF,$FF,0
.BYTE $C0,3,0
.BYTE $C0,3,0
.BYTE $C0,3,0
.BYTE $C0,3,0
.BYTE $C0,3,0
.BYTE $C0,3,0
.BYTE $C0,3,0
.BYTE $C0,3,0
.BYTE $C0,3,0
.BYTE $C0,3,0
.BYTE $C0,3,0
.BYTE $C0,3,0
.BYTE $FF,$FF,0
.BYTE $FF,$FF,0
.WORD 0,0,0,0,0,0,0,0
;
BILLTABM = 29
BILLTAB .ASC "ëùÜàSMÆßÃ”÷Õ’º“Ö–…—ƒ±CG EI¢"
UGROTAB .WORD CRUP,CRDOWN,CRLEFT,CRRIGHT,SELCHR,MENU
.WORD SLIDE,MIRROR,SLFONT,MIFONT
.WORD LOADR,SAVER,VERIFY,LMERGE
.WORD UNDO,COPYROM,RESETR,HELP,VERSION,INFO,QUIT
.WORD CHANGDEV,ERASESET,COPY,CMERGE,TOGGLE,ERASE
.WORD INVERT,INVFONT
;
TMP     *=*+2
TMP2    *=*+2
TMP3    *=*+2
CHRTMP  .WORD 0,0,0,0
REGIBAJT *=*+1
REGIBIT *=*+1
UJBAJT  *=*+1
UJBIT   *=*+1
REGIPOZ *=*+2
UNDOEN  *=*+1
CHRSAV  *=*+8
WINDPOZ *=*+1
DEVNUM  .BYTE 1
;
SCRTEXT .ASC "ì"
SCR2TEXT .ASC "í  ûCHAR ûFONT ûEDIT ûFILE ûMISC.  "
.ASC "; 12345678"
.BYTE 13
.ASC "----------------------------->1"
.BYTE 13
.ASC "BALI'S CHARACTER EDITOR  V2.0;2"
.BYTE 13
.ASC " (C) T*TH B+LINT (BALI) 1991.;3"
.BYTE 13
.ASC "----------------------------->4"
.BYTE 13
.ASC "   CURRENT CHARACTER:        ;5"
.BYTE 13
.ASC "----------------------------->6"
.BYTE 13
.ASC " ûF1 HELP  ûF3 SELECT  û^U UNDO ;7"
.BYTE 13
.ASC " ûF5 EDIT  ûF7 MENU    û^Q QUIT ;8"
.BYTE 13
.ASC "-----------------------------<----------"
.BYTE 0
;
QUITTEXT .ASC "ì	     BALI'S CHARACTER EDITOR  V2.0"
.BYTE 13
.ASC " (C)COPYRIGHT: TOTH BALINT (BALI) 1991. "
.ASC "BUDAPEST,KABAR U.7.V.27. (TEL:1873-943) "
.ASC "****************************************"
.ASC "öPROGRAM:       $0801  # 2049"
.BYTE 13
.ASC "CHARACTER SET: $2800  #10240"
.BYTE 13
.ASC "BASIC RAM:     $3001  #12289"
.BYTE 13
.ASC " 28671 BASIC BYTES FREE"
.BYTE 13
.ASC "RESTART EDITOR: SYS 2100"
.BYTE 13
.ASC "TURN USER'S CHARACTER SET ON:"
.BYTE 13
.ASC "POKE53272,27"
.BYTE 13
.ASC "TURN ROM CHARACTER SET ON:"
.BYTE 13
.ASC "POKE53272,21ëëëëëë"
.BYTE 13,0
;
SETIRQ SEI
LDA #%01111111
STA $DC0D
LDA #1
STA $D01A
LDA #RASZTER
STA $D012
LDA #%00011011
STA $D011
LDA #<UJIRQ
LDY #>UJIRQ
R028 STA CINV
STY CINV+1
CLI
RTS
;
CLRIRQ SEI
LDA #%10000001
STA $DC0D
LDA #%00011011
STA $D011
LDA #0
STA $D01A
STA $D020
STA $D015
LDA #%00010101
STA $D018
LDA #<REGIIRQ
LDY #>REGIIRQ
JMP R028
;
UJIRQ LDA $D012
CMP #RASZTER
BNE R010
LDA #%00011011
STA $D018
STA $D011
LDA #11
STA $D020
LDA #0
STA $D012
LDA #1
STA $D019
JMP ENDIRQ
;
R010 LDA #RASZTER
STA $D012
LDA #%00011001
STA $D018
LDA #0
STA $D020
LDA #%01011011
STA $D011
LDA #1
STA $D019
JMP REGIIRQ
;
PRINT STA $A3
STY $A4
LDY #0
STY TMP
R001 LDA ($A3),Y
BEQ R002
JSR CHROUT
INC TMP
LDY TMP
BNE R001
INC $A4
BNE R001
R002 RTS
;
SURE LDA #<SURETEXT
LDY #>SURETEXT
JSR PRINT
R003 JSR GETIN
CMP #"N"
BEQ R004
CMP #"Y"
BNE R003
R004 PHA
LDA #<TEXTCLR
LDY #>TEXTCLR
JSR PRINT
PLA
CMP #"Y"
RTS
TEXTCLR .ASC "  ûCHAR ûFONT ûEDIT ûFILE ûMISC.  "
.BYTE 0
SURETEXT .ASC "ARE YOU SURE? (Y/N)          "
.BYTE 0
;
PRINTSCR LDA #<SCRTEXT
LDY #>SCRTEXT
JSR PRINT
JSR SETIRQ
LDY #7
LDA #0
R030 STA INVRAM,Y
DEY
BPL R030
LDA #0
LDY #5
STA $A3
STY $A4
LDY #144
LDX #3
LDA #$80
R060 STA ($A3),Y
INY
BNE R060
INC $A4
DEX
BNE R060
LDA #<SETPOZ
LDY #>SETPOZ
STA $A3
STY $A4
LDX #0
R063 LDY #0
R061 TXA
INX
STA ($A3),Y
INY
INY
CPY #32
BNE R061
CLC
LDA $A3
ADC #80
STA $A3
BCC R062
INC $A4
R062 CPX #128
BNE R063
LDA #1
STA $D015
LDA #36
STA 2040
RTS
;
PRINTCHR LDX #5
LDY #22
CLC
JSR PLOT
LDX CN
CPX #100
BCS R005
LDA #"0"
JSR CHROUT
CPX #10
BCS R005
LDA #"0"
JSR CHROUT
R005 LDA #0
JSR LINPRT
JSR CHRKC
LDA #<HOMEPOZ
LDY #>HOMEPOZ
STA REGIPOZ
STY REGIPOZ+1
LDA #0
STA REGIBAJT
LDA #7
STA REGIBIT
LDA CN
AND #%01110000
CLC
ADC #126
STA $D001
LDA CN
ASL A
ASL A
ASL A
ASL A
LDX #0
CLC
ADC #52
BCC R080
INX
R080 STA $D000
STX $D010
RTS
;
CHRKC LDA CN
ASL A
ASL A
ASL A
STA $FD
LDA CN
ROL A
ROL A
ROL A
ROL A
AND #%00000011
ORA #>CHRRAM
STA $FE
RTS
;
DATA LDA #<SOR8POZ
LDY #>SOR8POZ
STA $A3
STY $A4
LDY #7
STY TMP
R009 LDA ($FD),Y
LDY #7
R006 LSR A
TAX
LDA #SPACE
BCC R007
LDA #INVSPACE
R007 STA ($A3),Y
TXA
DEY
BPL R006
SEC
LDA $A3
SBC #40
STA $A3
BCS R008
DEC $A4
R008 DEC TMP
LDY TMP
BPL R009
RTS
;
POZALL LDA REGIPOZ
LDY REGIPOZ+1
STA $FB
STY $FC
LDA REGIBAJT
STA UJBAJT
LDA REGIBIT
STA UJBIT
RTS
;
CRUP DEC UJBAJT
SEC
LDA $FB
SBC #40
STA $FB
BCS ELLPOZ
DEC $FC
JMP ELLPOZ
;
CRDOWN INC UJBAJT
CLC
LDA $FB
ADC #40
STA $FB
BCC ELLPOZ
INC $FC
JMP ELLPOZ
;
CRLEFT INC UJBIT
LDA $FB
BNE R013
DEC $FC
R013 DEC $FB
JMP ELLPOZ
;
CRRIGHT DEC UJBIT
INC $FB
BNE ELLPOZ
INC $FC
;
ELLPOZ LDA UJBAJT
CMP #8
BCS R015
LDA UJBIT
CMP #8
BCS R015
LDA $FB
LDY $FC
STA REGIPOZ
STY REGIPOZ+1
LDA UJBAJT
STA REGIBAJT
LDA UJBIT
STA REGIBIT
JMP CRSRON
R015 JMP LOOP
;
TOGGLE LDY REGIBAJT
LDX REGIBIT
LDA ($FD),Y
EOR XORTAB,X
STA ($FD),Y
RTS
XORTAB .BYTE 1,2,4,8,16,32,64,128
;
QUIT JSR SURE
BNE R015
JSR CLRIRQ
JSR MAKEINV
JSR IOINIT
JSR CLALL
JSR CINT
LDA #<QUITTEXT
LDY #>QUITTEXT
JSR PRINT
LDA #%10000000
JSR SETMSG
JMP READY
;
RESETR JSR SURE
BNE R015
JSR MAKEINV
JMP RESET
;
HELP LDA #<HELPTEXT
LDY #>HELPTEXT
UZENET JSR PRINT
R016 JSR GETIN
CMP #0
BEQ R016
JSR CLOSWIND
JMP LOOP
;
VERSION LDA #<VERSTEXT
LDY #>VERSTEXT
JMP UZENET
;
INFO LDA #<INFOTEXT
LDY #>INFOTEXT
JMP UZENET
;
SLUP LDY #0
LDA ($FD),Y
TAX
R018 INY
LDA ($FD),Y
DEY
STA ($FD),Y
INY
CPY #7
BNE R018
TXA
STA ($FD),Y
RTS
;
SLDOWN LDY #7
LDA ($FD),Y
TAX
R019 DEY
LDA ($FD),Y
INY
STA ($FD),Y
DEY
BNE R019
TXA
STA ($FD),Y
RTS
;
SLLEFT LDY #7
R020 LDA ($FD),Y
ROL A
LDA ($FD),Y
ROL A
STA ($FD),Y
DEY
BPL R020
RTS
;
SLRIGHT LDY #7
R021 LDA ($FD),Y
ROR A
LDA ($FD),Y
ROR A
STA ($FD),Y
DEY
BPL R021
RTS
;
MHORIZ LDY #0
LDX #7
R022 LDA ($FD),Y
STA CHRTMP,X
INY
DEX
BPL R022
COPTMPC LDY #7
R023 LDA CHRTMP,Y
STA ($FD),Y
DEY
BPL R023
RTS
;
MVERT LDY #7
R024 LDA #7
STA TMP
LDA ($FD),Y
R025 ASL A
PHA
TXA
ROR A
TAX
PLA
DEC TMP
BPL R025
TXA
STA ($FD),Y
DEY
BPL R024
RTS
;
INVERT LDY #7
R026 LDA ($FD),Y
EOR #$FF
STA ($FD),Y
DEY
BPL R026
RTS
;
ERASE JSR SAV1CHR
LDY #7
LDA #0
R027 STA ($FD),Y
DEY
BPL R027
RTS
;
SAV1CHR LDA #$FF
STA UNDOEN
LDY #7
R055 LDA ($FD),Y
STA CHRSAV,Y
DEY
BPL R055
RTS
;
ERASESET JSR SAVECHR
LDA #0
LDY #>CHRRAM
STA $A3
STY $A4
TAY
LDX #4
R053 STA ($A3),Y
INY
BNE R053
INC $A4
DEX
BNE R053
RTS
;
SELR JSR PRINT
R064 JSR GETIN
CMP #13
BEQ R065
CMP #" "
BEQ R065
CMP #"á"
BEQ R065
CMP #""
BEQ R066
CMP #"ù"
BEQ R067
CMP #"ë"
BEQ R068
CMP #""
BNE R064
LDA CN
CLC
ADC #16
BNE R069
R068 LDA CN
SEC
SBC #16
JMP R069
R066 INC CN
INC CN
R067 DEC CN
LDA CN
R069 AND #%01111111
STA CN
JSR PRINTCHR
JSR DATA
JMP R064
R065 LDA #<TEXTCLR
LDY #>TEXTCLR
JSR PRINT
RTS
;
SELCHR LDA #<SCHRTEXT
LDY #>SCHRTEXT
JSR SELR
JMP LOOP
SCHRTEXT .ASC "SELECT CHARACTER             "
.BYTE 0
;
MAKEINV LDA #0
LDY #>INVRAM
STA $4B
STY $4C
LDY #>CHRRAM
STA $A3
STY $A4
LDX #4
TAY
R031 LDA ($A3),Y
EOR #$FF
STA ($4B),Y
INY
BNE R031
INC $A4
INC $4C
DEX
BNE R031
RTS
;
FILNAM LDA #0
STA UNDOEN
LDA #<FILNTEXT
LDY #>FILNTEXT
JSR PRINT
LDX #9
LDA #" "
R032 STA FILENAME,X
DEX
BPL R032
LDX #0
LDY #16
CLC
JSR PLOT
LDA #0
STA BLNSW
STA TMP2
R033 JSR STOP
BNE R040
LDA #<TEXTCLR
LDY #>TEXTCLR
JSR PRINT
JMP LOOP
R040 JSR GETIN
LDY TMP2
CMP #"à"
BEQ R034
CMP #13
BEQ R035
CMP #20
BEQ R036
CMP #" "
BCC R033
CMP #96
BCS R033
CPY #10
BEQ R033
INC TMP2
STA FILENAME,Y
JSR CHROUT
JMP R033
R036 CPY #0
BEQ R033
LDA #"ù"
JSR CHROUT
LDY TMP2
LDA #" "
STA SCRRAM+16,Y
DEC TMP2
DEY
STA SCRRAM+16,Y
STA FILENAME,Y
JMP R033
R034 LDY #9
R037 LDA FILNTMP,Y
STA FILENAME,Y
DEY
BPL R037
R035 LDA #1
STA BLNSW
LDY #9
R038 LDA FILENAME,Y
STA FILNTMP,Y
DEY
BPL R038
LDX DEVNUM
LDA #1
LDY #0
JSR SETLFS
LDA #15
LDX #<FILENAME
LDY #>FILENAME
JSR SETNAM
LDA #"ì"
JSR CHROUT
JSR CLRIRQ
JSR MAKEINV
LDX #0
STX $D015
LDY #>CHRRAM
RTS
FILENAME .ASC "           .CHR"
FILNTMP .ASC "          "
FILNTEXT .ASC "ENTER FILENAME:              "
.BYTE 0
;
SAVER JSR FILNAM
STX $4B
STY $4C
LDY #>BASBOT
LDA #$4B
JSR SAVE
BCC R042
BCS R043
;
LOADR JSR FILNAM
TXA
JSR LOAD
BCS R043
JSR READST
AND #%10111111
BEQ R042
R046 LDA #1
R043 ASL A
TAX
LDA ERRTAB,X
LDY ERRTAB+1,X
JSR PRINT
LDA #0
STA NDX
R044 JSR GETIN
CMP #0
BEQ R044
R042 LDA #0
STA NDX
JMP UJKEP
;
VERIFY JSR FILNAM
LDA #1
JSR LOAD
BCS R043
JSR READST
AND #%00010000
BEQ R045
LDA #2
BNE R043
R045 LDA #3
BNE R043
;
LMERGE JSR FILNAM
LDY #$A0
TXA
JSR LOAD
BCS R043
JSR READST
AND #%10111111
BNE R046
LDA #%00110110
STA 1
LDA #0
LDY #$A0
STA $A3
STY $A4
LDY #>CHRRAM
STA $4B
STY $4C
TAY
LDX #4
R047 LDA ($A3),Y
ORA ($4B),Y
STA ($4B),Y
INY
BNE R047
INC $A4
INC $4C
DEX
BNE R047
LDA #%00110111
STA 1
JMP R042
ERRTAB .WORD STOPTEXT,LOERTEXT,VEERTEXT
.WORD VEOKTEXT,NOTFTEXT,DRIVTEXT
STOPTEXT .BYTE 13
.ASC "OPERATION TERMINATED BY THE STOP KEY"
.BYTE 0
LOERTEXT .BYTE 13
.ASC "ERROR DURING LOAD"
.BYTE 0
VEERTEXT .BYTE 13
.ASC "VERIFY ERROR"
.BYTE 13
.ASC "PLEASE TRY TO SAVE IT AGAIN!"
.BYTE 0
VEOKTEXT .BYTE 13
.ASC "OK"
.BYTE 0
NOTFTEXT .BYTE 13
.ASC "FILE NOT FOUND"
.BYTE 0
DRIVTEXT .BYTE 13
.ASC "DRIVE NOT PRESENT"
.BYTE 0
;
COPY1K LDA #0
STA $A3
STX $A4
STA $4B
STY $4C
TAY
LDX #4
R048 LDA ($A3),Y
STA ($4B),Y
INY
BNE R048
INC $A4
INC $4C
DEX
BNE R048
RTS
;
SAVECHR LDX #>CHRRAM
LDY #$A8
JSR COPY1K
LDA #1
STA UNDOEN
RTS
;
COPYROM JSR SAVECHR
LDA #<WSETTEXT
LDY #>WSETTEXT
JSR PRINT
R049 JSR GETIN
CMP #"1"
BCC R049
CMP #"3"
BCS R049
PHA
LDA #<TEXTCLR
LDY #>TEXTCLR
JSR PRINT
SEI
LDA #%00110011
STA 1
PLA
LDX #$D0
CMP #"1"
BEQ R050
LDX #$D8
R050 LDY #>CHRRAM
JSR COPY1K
LDA #%00110111
STA 1
CLI
JMP RAJZOL
WSETTEXT .ASC "WHICH CHARACTER SET? (1/2)   "
.BYTE 0
;
UNDO LDA UNDOEN
BEQ R051
CMP #$FF
BEQ R052
LDA #%00110110
STA 1
LDX #$A8
LDY #>CHRRAM
JSR COPY1K
LDA #%00110111
STA 1
JMP RAJZOL
R052 LDY #7
R056 LDA CHRSAV,Y
STA ($FD),Y
DEY
BPL R056
JMP RAJZOL
R051 JMP CRSRON
;
SELDEST JSR PRINT
LDA CN
STA TMP3
LDA #<COPYTEXT
LDY #>COPYTEXT
JSR SELR
LDA #<CURTEXT
LDY #>CURTEXT
JSR PRINT
JSR SAV1CHR
LDA CN
PHA
LDA TMP3
STA CN
JSR CHRKC
LDA $FD
LDY $FE
STA $A3
STY $A4
PLA
STA CN
JSR CHRKC
LDY #7
RTS
COPYTEXT .ASC "SELECT DESTINATION CHARACTER "
.BYTE 0
CURTEXT .ASC "   CURRENT"
.BYTE 0
;
COPY LDA #<CPTOTEXT
LDY #>CPTOTEXT
JSR SELDEST
R070 LDA ($A3),Y
STA ($FD),Y
DEY
BPL R070
RTS
CPTOTEXT .ASC "   COPY TO"
.BYTE 0
;
CMERGE LDA #<MGTOTEXT
LDY #>MGTOTEXT
JSR SELDEST
R071 LDA ($A3),Y
ORA ($FD),Y
STA ($FD),Y
DEY
BPL R071
RTS
MGTOTEXT .ASC "  MERGE TO"
.BYTE 0
;
SLMI JSR GETIN
CMP #13
BEQ R075
CMP #" "
BEQ R075
CMP #"á"
BEQ R075
CMP #""
BEQ R076
CMP #"ù"
BEQ R077
CMP #"ë"
BEQ R078
CMP #""
BNE SLMI
LDX #2
RTS
R078 LDX #0
RTS
R076 LDX #6
RTS
R077 LDX #4
RTS
R075 LDA #<TEXTCLR
LDY #>TEXTCLR
JSR PRINT
LDA #$FF
RTS
SLIDTEXT .ASC "SLIDE CHARACTER              "
.BYTE 0
;
MIRROR LDA #<MIRRTEXT
LDY #>MIRRTEXT
JSR PRINT
JSR SAV1CHR
R081 JSR SLMI
BMI R086
LDA MITAB,X
STA TMP2
LDA MITAB+1,X
STA TMP2+1
LDA #>(R112-1)
PHA
LDA #<(R112-1)
PHA
JMP (TMP2)
R112 JSR DATA
JMP R081
R086 JMP LOOP
MIRRTEXT .ASC "MIRROR CHARACTER             "
.BYTE 0
MITAB .WORD MHORIZ,MHORIZ,MVERT,MVERT
;
SLIDE LDA #<SLIDTEXT
LDY #>SLIDTEXT
JSR PRINT
JSR SAV1CHR
R082 JSR SLMI
BMI R086
LDA SLTAB,X
STA TMP2
LDA SLTAB+1,X
STA TMP2+1
LDA #>(R083-1)
PHA
LDA #<(R083-1)
PHA
JMP (TMP2)
R083 JSR DATA
JMP R082
SLTAB .WORD SLUP,SLDOWN,SLLEFT,SLRIGHT
;
FOMENU LDY #6
LDX WINDPOZ
DEX
R087 LDA SCRRAM,X
EOR #%01000000
STA SCRRAM,X
INX
DEY
BNE R087
RTS
;
MENU JSR FOMENU
R088 JSR GETIN
CMP #0
BEQ R088
CMP #""
BNE R089
JSR FOMENU
LDA WINDPOZ
CLC
ADC #5
CMP #27
BNE R090
LDA #2
R090 STA WINDPOZ
JMP MENU
R089 CMP #"ù"
BNE R091
JSR FOMENU
LDA WINDPOZ
SEC
SBC #5
BCS R092
LDA #22
R092 STA WINDPOZ
JMP MENU
R091 CMP #13
BEQ R094
PHA
JSR FOMENU
PLA
JMP BILLELL
R094 LDA WINDPOZ
JSR OPENWIND
LDA #0
R101 STA TMP2
R102 JSR MENUPONT
R099 JSR GETIN
CMP #0
BEQ R099
CMP #""
BNE R100
JSR MENUPONT
INC TMP2
LDA $4E
CMP TMP2
BNE R102
LDA #0
JMP R101
R100 CMP #"ë"
BNE R103
JSR MENUPONT
DEC TMP2
BPL R102
LDA $4E
STA TMP2
DEC TMP2
JMP R102
R103 PHA
JSR CLOSWIND
PLA
CMP #""
BNE R104
LDA WINDPOZ
CLC
ADC #5
CMP #27
BNE R105
LDA #2
R105 STA WINDPOZ
JSR FOMENU
JMP R094
R104 CMP #"ù"
BNE R106
LDA WINDPOZ
SEC
SBC #5
BCS R107
LDA #22
R107 STA WINDPOZ
JSR FOMENU
JMP R094
R106 CMP #13
BEQ R108
JMP BILLELL
R108 LDA WINDPOZ
LSR A
LSR A
TAY
LDA OSZTTAB,Y
ORA TMP2
TAY
LDA HOTKTAB,Y
JMP BILLELL
OSZTTAB .BYTE 0,8,0,16,24,32
HOTKTAB .ASC "SMIECGááÆß¢±ºááááÜááááááÃÕ”÷ƒááá’Ö–…—“áá"
;
OPENWIND LDY #0
LDX WINDPOZ
R095 LDA WINDDATA-2,X
STA $4C,Y
INX
INY
CPY #4
BNE R095
LDA #0
STA TMP+1
LDA #$FF
STA TMP
LDA #""
JSR CHROUT
R096 INC TMP+1
LDY WINDPOZ
DEY
LDX TMP+1
CLC
JSR PLOT
R097 INC TMP
LDY TMP
LDA ($4C),Y
CMP #0
BEQ R098
CMP #13
BEQ R096
JSR CHROUT
JMP R097
R098 LDA #"í"
JSR CHROUT
RTS
;
MENUPONT LDY TMP2
LDA MENUSTAB,Y
STA $A3
LDA #>SCRRAM
STA $A4
CPY #5
BCC R109
INC $A4
R109 LDA $A3
CLC
ADC WINDPOZ
STA $A3
BCC R200
INC $A4
R200 DEC $A3
LDA $A3
CMP #$FF
BNE R110
DEC $A4
R110 LDY $4F
R111 LDA ($A3),Y
EOR #%01000000
STA ($A3),Y
DEY
BNE R111
RTS
MENUSTAB .BYTE 80,120,160,200,240,24,64
;
CLOSWIND LDA #<SCR2TEXT
LDY #>SCR2TEXT
JSR PRINT
JSR PRINTCHR
JSR DATA
LDA #<HOMEPOZ
LDY #>(HOMEPOZ+$D400)
STA $4C
STY $4D
LDX #7
R122 LDA #1
LDY #7
R120 STA ($4C),Y
DEY
BPL R120
CLC
LDA $4C
ADC #40
STA $4C
BCC R121
INC $4D
R121 DEX
BPL R122
RTS
;
CHANGDEV LDA DEVNUM
EOR #%00001001
STA DEVNUM
LSR A
TAY
LDX #0
R123 LDA TAPEDISK,Y
STA DEVNAME,X
INY
INX
CPX #4
BNE R123
RTS
TAPEDISK .ASC "TAPEDISK"
;
SLFONT LDA #<SLFOTEXT
LDY #>SLFOTEXT
JSR PRINT
JSR SAVECHR
R130 JSR SLMI
BMI R131
LDA SLTAB,X
STA TMP2
LDA SLTAB+1,X
STA TMP2+1
LDA CN
STA TMP3
LDA #0
STA TMP3+1
R132 STA CN
JSR CHRKC
LDA #>(R133-1)
PHA
LDA #<(R133-1)
PHA
JMP (TMP2)
R133 INC TMP3+1
LDA TMP3+1
BPL R132
LDA TMP3
STA CN
JSR CHRKC
JSR DATA
JMP R130
R131 JMP CRSRON
SLFOTEXT .ASC "SLIDE FONT                   "
.BYTE 0
;
MIFONT LDA #<MIFOTEXT
LDY #>MIFOTEXT
JSR PRINT
JSR SAVECHR
R134 JSR SLMI
BMI R131
LDA MITAB,X
STA TMP2
LDA MITAB+1,X
STA TMP2+1
LDA CN
STA TMP3
LDA #0
STA TMP3+1
R136 STA CN
JSR CHRKC
LDA #>(R137-1)
PHA
LDA #<(R137-1)
PHA
JMP (TMP2)
R137 INC TMP3+1
LDA TMP3+1
BPL R136
LDA TMP3
STA CN
JSR CHRKC
JSR DATA
JMP R134
MIFOTEXT .ASC "MIRROR FONT                  "
.BYTE 0
;
INVFONT JSR SAVECHR
LDA #<CHRRAM
LDY #>CHRRAM
STA $A3
STY $A4
LDX #4
LDY #0
R138 LDA ($A3),Y
EOR #%11111111
STA ($A3),Y
INY
BNE R138
INC $A4
DEX
BNE R138
RTS
;
WINDDATA .WORD CHARWIND
.BYTE 6,9,0
.WORD FONTWIND
.BYTE 5,11,0
.WORD EDITWIND
.BYTE 2,10,0
.WORD FILEWIND
.BYTE 5,11,0
.WORD MISCWIND
.BYTE 6,10,0
CHARWIND .ASC "û!==CHAR===#"
.BYTE 13
.ASC "[SöLIDE   Sû]†"
.BYTE 13
.ASC "[MöIRROR  Mû]†"
.BYTE 13
.ASC "[IöNVERT  Iû]†"
.BYTE 13
.ASC "[EöRASE   Eû]†"
.BYTE 13
.ASC "[CöOPY    Cû]†"
.BYTE 13
.ASC "[MöERGE   Gû]†"
.BYTE 13
.ASC "$%%%%%%%%%&†"
.BYTE 13
.ASC "†††††††††††"
.BYTE 0
FONTWIND .ASC "û!===FONT====#"
.BYTE 13
.ASC "[SöLIDE    _Sû]†"
.BYTE 13
.ASC "[MöIRROR   _Mû]†"
.BYTE 13
.ASC "[IöNVERT   _Iû]†"
.BYTE 13
.ASC "[EöRASE    _Eû]†"
.BYTE 13
.ASC "[CöOPY ROM _Cû]†"
.BYTE 13
.ASC "$%%%%%%%%%%%&†"
.BYTE 13
.ASC "†††††††††††††"
.BYTE 0
EDITWIND .ASC "û!===EDIT===#"
.BYTE 13
.ASC "[EöDIT    F5û]†"
.BYTE 13
.ASC "[SöELECT  F3û]†"
.BYTE 13
.ASC "$%%%%%%%%%%&†"
.BYTE 13
.ASC "††††††††††††"
.BYTE 0
FILEWIND .ASC "û!===FILE====#"
.BYTE 13
.ASC "[LöOAD     ^Sû]†"
.BYTE 13
.ASC "[MöERGE    ^Mû]†"
.BYTE 13
.ASC "[SöAVE     ^Sû]†"
.BYTE 13
.ASC "[VöERIFY   ^Vû]†"
.BYTE 13
.ASC "[CöHANGE D.^Dû]†"
.BYTE 13
.ASC "[öDEVICE:"
DEVNAME .ASC "TAPEû]†"
.BYTE 13
.ASC "$%%%%%%%%%%%&†"
.BYTE 13
.ASC "†††††††††††††"
.BYTE 0
MISCWIND .ASC "û!===MISC===#"
.BYTE 13
.ASC "[UöNDO    ^Uû]†"
.BYTE 13
.ASC "[HöELP    F1û]†"
.BYTE 13
.ASC "[VöERSION ^Pû]†"
.BYTE 13
.ASC "[IöNFO    ^Iû]†"
.BYTE 13
.ASC "[QöUIT    ^Qû]†"
.BYTE 13
.ASC "[RöESET   ^Rû]†"
.BYTE 13
.ASC "$%%%%%%%%%%&†"
.BYTE 13
.ASC "††††††††††††"
.BYTE 0
;
HELPTEXT .ASC "û"
.ASC "!===============HELP================#"
.BYTE 13
.ASC "[    F5 öEDIT MODE                   û]†"
.BYTE 13
.ASC "[  CRSR öMOVE†CURSOR   û!===========# ]†"
.BYTE 13
.ASC "[ SPACE öTOGGLE DOT    û[YOU CAN USEû]†]†"
.BYTE 13
.ASC "[---------------------û[THE HOTKEYSû]†]†"
.BYTE 13
.ASC "[    F7 öMENU MODE     û[EVERYWHERE û]†]†"
.BYTE 13
.ASC "[  CRSR öMOVE LIGHTBAR û$%%%%%%%%%%%&†]†"
.BYTE 13
.ASC "[RETURN öCHOOSE ITEM    †††††††††††††û]†"
.BYTE 13
.ASC "$%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%&†"
.BYTE 13
.ASC "†††††††††††††††††††††††††††††††††††††"
.BYTE 0
;
VERSTEXT .ASC "û"
.ASC "!==========PROGRAM INFO==========#"
.BYTE 13
.ASC "[    BALI'S CHARACTER EDITOR     û]†"
.BYTE 13
.ASC "[          VERSION 2.0           û]†"
.BYTE 13
.ASC "[öCOPYRIGHT (C) 1991. T*TH B+LINT û]†"
.BYTE 13
.ASC "[ö 1039 BUDAPEST  KABAR U.7.V.27. û]†"
.BYTE 13
.ASC "[ö HUNGARY  (TELEPHONE: 1873 943) û]†"
.BYTE 13
.ASC "$%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%&†"
.BYTE 13
.ASC "††††††††††††††††††††††††††††††††††"
.BYTE 0
;
INFOTEXT .ASC "û"
.ASC "!===============INFO===============#"
.BYTE 13
.ASC "[ö RESTART AFTER QUIT: SYS 2100     û]†"
.BYTE 13
.ASC "[ö USER'S CHARACTERS:  POKE53272'25 û]†"
.BYTE 13
.ASC "[ö ROM CHARACTERS:     POKE53272'21 û]†"
.BYTE 13
.ASC "[-------------MEMORY MAP-----------û]†"
.BYTE 13
.ASC "[ö   THIS PROGRAM   0801  ( 2049)   û]†"
.BYTE 13
.ASC "[ö   CHARACTER SET  2800  (10240)   û]†"
.BYTE 13
.ASC "[ö   BASIC RAM      3001  (12289)   û]†"
.BYTE 13
.ASC "$%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%&†"
.BYTE 13
.ASC "††††††††††††††††††††††††††††††††††††"
.BYTE 0
