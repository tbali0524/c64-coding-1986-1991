SYS36864
.OPT OO
;
; *********************************
;      BALI'S SPRITE DESIGNER
;              V4.0           .ASM
;  ASSEMBLY FORRASKOD  $2001-$8FFF
;  GEPI KODU TARGYKOD  $0801-$1FFF
;   FORDITOPROGRAM    PROFI-ASS 64
;  UTOLSO MODOSITAS 1990. SZEPT. 9.
;    (C)1990. TOTH BALINT (BALI)
;   1039 BUDAPEST,KABAR U.7.V.27.
;    HUNGARY  ( TEL. 1873-943 )
; *********************************
;
; BASIC ROM RUTINOK
;
LINPRT   = $BDCD ; DECIMALIS KIIRAS
SCRTCH   = $A644 ; NEW
READY    = $A474 ; BELEPES BASIC-BE
;
; KERNAL ROM RUTINOK
;
CINT     = $FF81
IOINIT   = $FF84
MEMBOT   = $FF9C
SETLFS   = $FFBA
SETNAM   = $FFBD
CHROUT   = $FFD2
LOAD     = $FFD5
SAVE     = $FFD8
STOP     = $FFE1
GETIN    = $FFE4
CLALL    = $FFE7
PLOT     = $FFF0
SETMSG   = $FF90
READST   = $FFB7
;
; RENDSZER TERULETEK
;
VIC      = $D000
SCRRAM   = $0400
COLRAM   = $D800
SN       = 2   ; SPR. SORSZAM
NDX      = 198 ; BILL. PUFFER MERET
COLOR    = 646
SPRMUT   = 2040
OSPTAB   = $2E80
ASPTAB   = $2E90
FSTTAB   = $2EA0
LSTTAB   = $2EB0
CMODTAB  = $2EC0
BGRTAB   = $2F00
MC1TAB   = $2F40
SPRTAB   = $2F80
MC2TAB   = $2FC0
SPRTMP   = $2E00
SZERKKC  = SCRRAM+(3*40)+15
COLPOZ   = COLRAM+(15*40)+11
;
; BASIC INDITO ( SYS2180 )
;
*= $0801
.BYTE $35,8,$C6,7,$9E,"2","1"
.BYTE "8","0",$3A,$8F,$22,20,20,20
.BYTE 20,20,20,20,20,20,20,20,20
.ASC "BALI'S SPRITE DESIGNER V4.0"
.BYTE 0,0,0
;
; HATTER SPRITE ( MUTATO 33 )
;
*= $0840
.BYTE $FF,$FF,0,$FF,$FF,0
.BYTE $FF,$FF,0,$FF,$FF,0
.BYTE $FF,$FF,0,$FF,$FF,0
.BYTE $FF,$FF,0,$FF,$FF,0
.BYTE $FF,$FF,0,$FF,$FF,0
.BYTE $FF,$FF,0,$FF,$FF,0
.BYTE $FF,$FF,0,$FF,$FF,0
.WORD 0,0,0,0,0,0,0,0,0,0,0
;
; ********************************
;        F O P R O G R A M
; ********************************
;
*= 2180      ; = $0884
;
; BASIC RAM BEALLITAS, NEW
;
LDA #0
STA $4000
CLC
LDX #1
LDY #$40
STX 43
STY 44
JSR MEMBOT
JSR SCRTCH   ; NEW
;
; INICIALIZALAS
;
; BELEPESI PONT  SYS2200 ($0898)
;
INIT JSR IOINIT
JSR CLALL
JSR CINT
LDA #0
STA SN
STA OBJ
STA VIC+32   ; KERETSZIN
LDA #101
STA VIC      ; SPR X
LDA #107
STA VIC+1    ; SPR Y
LDA #96
STA VIC+4    ; SPR HATTER X
LDA #104
STA VIC+5    ; SPR HATTER Y
LDA #%00000100
STA VIC+23   ; DUPLA X
STA VIC+29   ; DUPLA Y
LDA #33
STA SPRMUT+2
LDA #11
STA VIC+33   ; HATTERSZIN
LDA #0
STA UNDOEN
LDA #%11000000
JSR SETMSG
LDA #<CLRTEXT
LDY #>CLRTEXT
JSR YNKERDES
BNE SPRON
LDA #0
LDY #$2E
STA $FD
STY $FE
TAY
LDX #$40
C002 STA ($FD),Y
INY
BNE C002
INC $FE
CPX $FE
BNE C002
LDA #3
LDY #$BF
C004 LDX #$40
C003 STA $2F00,Y
DEY
DEX
BNE C003
SEC
SBC #1
BNE C004
LDA #1
LDY #$3F
C001 STA CMODTAB,Y
DEY
BPL C001
LDY #$1F
LDA #50
C008 STA OSPTAB,Y
DEY
BPL C008
SPRON LDA #%00000101
STA VIC+21   ; SPR BEKAPCSOLAS
LDA #<SCRTEXT
LDY #>SCRTEXT
JSR PRINT
JSR OBJPRINT
UJSPR  JSR HOME
UJGRID JSR GRID
SPRNUM JSR SNPRINT
JSR JELAKTSZ
LDX SN
LDA CMODTAB,X
BEQ RAJZOL
LDA REGIBIT
LSR A
BCS RAJZOL
INC REGIBIT
DEC REGIPOZ
LDA REGIPOZ
CMP #$FF
BNE RAJZOL
DEC REGIPOZ+1
RAJZOL JSR DATA
LOOP JSR POZALL
CRSR LDY #0
LDA ($FB),Y
STA TMP
LDA #209
STA ($FB),Y
C005 JSR GETIN
CMP #0
BEQ C005
TAX
LDY #0
LDA TMP
STA ($FB),Y
TXA
BILLELL LDX #BILLTABM
C007 DEX
BMI CRSR
CMP BILLTAB,X
BNE C007
PHA
TXA
ASL A
TAX
LDA UGROTAB,X
STA TMP
LDA UGROTAB+1,X
STA TMP+1
PLA
JMP (TMP)
;
; FUNKCIOBILLENTYUK TABLAZATA
;       RUTINCIMEKKEL
BILLTABM = 42
BILLTAB .ASC "—UDLRGCX+-ì!"
.BYTE 34
.ASC "#$HIYOëù’1234 √MÃ÷”Õ€›FEA…"
UGROTAB .WORD QUIT,SLUP,SLDOWN,SLLEFT,SLRIGHT,CHGRID,CHCOLMOD
.WORD MIRRORX,SNNOV,SNCSOK,SPRERASE,CRSRHOME,SETCOL
.WORD SETCOL,SETCOL,SETCOL,HELP,INVERT
.WORD MIRRORY,ROTATE,UP,DOWN,LEFT,RIGHT,UNDO,SETDOT
.WORD SETDOT,SETDOT,SETDOT,SPACE,COPY,CMERGE
.WORD LOADR,VERIFYR,SAVER,LMERGE,OBJNOV,OBJCSOK
.WORD FSTEP,LSTEP,ANIMATE,INIT
;
; VALTOZOK MUNKATERULETE
;
TMP      *=*+2
TMP2     *=*+2
TMP3     *=*+2
AKTSZIN  .BYTE 2
AKTBGR   *=*+1
AKTMC1   *=*+1
AKTSPRC  *=*+1
AKTMC2   *=*+1
GRIDON   .BYTE 1
OBJ      *=*+1
REGIPOZ  *=*+2
REGIBAJT *=*+1
UJBAJT   *=*+1
REGIBIT  *=*+1
UJBIT    *=*+1
UNDOEN   *=*+1
SZINTMP  *=*+4
CMODTMP  *=*+1
;
; KERDES ADATMEZO TORLESEROL
;
CLRTEXT .BYTE 8,142
.ASC "ìíCLEAR SPRITE AREA ? (Y/N)"
.BYTE 13
.ASC "û(WARNING:ALL SPRITES WILL BE LOST!)"
.BYTE 0
;
; *******************************
;     S Z U B R U T I N O K
; *******************************
;
;
; PRINT (A/Y SZOVEG KIIRASA 0-IG)
;
PRINT STA $FD
STY $FE
LDY #0
STY TMP
R001 LDA ($FD),Y
BEQ R002
JSR CHROUT
INC TMP
LDY TMP
BNE R001
INC $FE
JMP R001
R002 RTS
;
; Y-N KERDES
;
YNKERDES JSR PRINT
R003 JSR GETIN
CMP #"N"
BEQ R004
CMP #"Y"
BNE R003
R004 PHA
LDA #<TEXTCLR
LDY #>TEXTCLR
JSR PRINT
PLA
CMP #"Y"
RTS
TEXTCLR .ASC "                                       "
.BYTE 0
;
; FOKBEOSZTAS KIRAJZOLASA
;
GRID LDX #2
LDY #15
CLC
JSR PLOT
LDX SN
LDA CMODTAB,X
AND #%00000001
STA CMODTAB,X
BEQ R005
LDA #<MULGRID
LDY #>MULGRID
LDX #247
JMP R006
R005 LDA #<HIRGRID
LDY #>HIRGRID
LDX #207
R006 STX TMP2
JSR PRINT
LDA #207
STA TMP2+1
LDA GRIDON
BNE R021
LDA #160
STA TMP2
STA TMP2+1
R021 LDA #<SZERKKC
LDY #>SZERKKC
STA $FD
STY $FE
LDX #21
R007 LDY #0
R008 LDA TMP2+1
STA ($FD),Y
INY
LDA TMP2
STA ($FD),Y
INY
CPY #24
BNE R008
CLC
LDA $FD
ADC #40
BCC R009
INC $FE
R009 STA $FD
DEX
BNE R007
RTS
MULGRID .ASC "1 2 3 4 5 6 7 8 9 0 1 2 "
.BYTE 0
HIRGRID .ASC "123456789012345678901234"
.BYTE 0
;
; SPRITE SORSZAM,SZINEK KIIRASA
;
SNPRINT LDA SN
CLC
ADC #192
STA SPRMUT
CLC
LDX #9
LDY #3
JSR PLOT
LDX SN
JSR XPRINT
CLC
LDX #12
LDY #2
JSR PLOT
LDX SN
LDA CMODTAB,X
BEQ R011
LDA #1
STA VIC+28
LDA #<MULTEXT
LDY #>MULTEXT
JMP R012
R011 LDA #0
STA VIC+28
LDA #<HIRTEXT
LDY #>HIRTEXT
R012 JSR PRINT
JSR COLPRINT
RTS
MULTEXT .ASC "MULTI 4"
.BYTE 0
HIRTEXT .ASC "HI-RES2"
.BYTE 0
;
; KIIRJA X-ET
;
XPRINT CPX #10
BCS R010
LDA #"0"
JSR CHROUT
R010 LDA #0
JSR LINPRT
RTS
;
; SZINEK KIIRASA
;
COLPRINT LDA #3
STA TMP
R013 LDY TMP
LDX SORTAB,Y
LDY #9
CLC
JSR PLOT
LDX TMP
LDA COLTABLO,X
CLC
ADC SN
TAY
LDA BGRTAB,Y
AND #%00001111
STA BGRTAB,Y
LDY RELTAB,X
STA COLPOZ,Y
INY
STA COLPOZ,Y
LDY VICCTAB,X
STA VIC,Y
STA AKTBGR,X
TAX
JSR XPRINT
DEC TMP
BPL R013
RTS
SORTAB   .BYTE 15,16,17,18
COLTABLO .BYTE 0,$40,$80,$C0
RELTAB   .BYTE 0,40,80,120
VICCTAB  .BYTE 41,37,39,38
;
; SPRITE KEZDOCIM $FD/$FE -BE
;
SPRKC LDA SN
LSR A
LSR A
CLC
ADC #$30
STA $FE
LDA SN
ROR A
ROR A
ROR A
AND #%11000000
STA $FD
RTS
;
; SPRITE KIRAJZOLAS
;
DATA LDA #<SZERKKC
LDY #>COLRAM
STA $A3
STY $A4
JSR SPRKC
LDA #21
STA TMP2
LDY #0
LDX SN
LDA CMODTAB,X
BNE R022
; *** HIRES
R015 LDA #3
STA TMP
R016 LDX #7
LDA ($FD),Y
R017 ASL A
PHA
LDA AKTBGR
BCC R018
LDA AKTSPRC
R018 STA ($A3),Y
INC $A3
BNE R019
INC $A4
R019 PLA
DEX
BPL R017
INC $FD
DEC TMP
BNE R016
CLC
LDA $A3
ADC #16
STA $A3
BCC R020
INC $A4
R020 DEC TMP2
BNE R015
RTS
; *** MULTI
R022 LDA #3
STA TMP
R023 LDA #3
STA TMP+1
LDA ($FD),Y
R024 ROL A
ROL A
PHA
ROL A
AND #%00000011
TAX
LDA AKTBGR,X
STA ($A3),Y
INY
STA ($A3),Y
DEY
CLC
LDA $A3
ADC #2
STA $A3
BCC R025
INC $A4
R025 PLA
DEC TMP+1
BPL R024
INC $FD
DEC TMP
BNE R023
CLC
LDA $A3
ADC #16
STA $A3
BCC R026
INC $A4
R026 DEC TMP2
BNE R022
RTS
;
; KILEPES A PROGRAMBOL
;
QUIT LDA #<SURETEXT
LDY #>SURETEXT
JSR YNKERDES
BNE R014
JSR IOINIT
JSR CLALL
JSR CINT
LDA #<QUITTEXT
LDY #>QUITTEXT
JSR PRINT
LDA #%10000000
JSR SETMSG
JMP READY
R014 JMP LOOP
SURETEXT .ASC "ARE YOU SURE ? (Y/N)"
.BYTE 0
;
; KURZOR ALAPHELYZETBE
;
HOME LDA #<SZERKKC
STA REGIPOZ
LDA #>SZERKKC
STA REGIPOZ+1
LDA #0
STA REGIBAJT
LDA #7
STA REGIBIT
RTS
;
; SLIDE DOWN
;
SLDOWN JSR SPRKC
LDY #$3E
R027 LDA ($FD),Y
PHA
DEY
CPY #$3B
BNE R027
R028 LDA ($FD),Y
INY
INY
INY
STA ($FD),Y
DEY
DEY
DEY
DEY
BPL R028
INY
R029 PLA
STA ($FD),Y
INY
CPY #3
BNE R029
JMP RAJZOL
;
; SLIDE UP
;
SLUP JSR SPRKC
LDY #0
R030 LDA ($FD),Y
PHA
INY
CPY #3
BNE R030
R031 LDA ($FD),Y
DEY
DEY
DEY
STA ($FD),Y
INY
INY
INY
INY
CPY #$3F
BNE R031
DEY
R032 PLA
STA ($FD),Y
DEY
CPY #$3B
BNE R032
JMP RAJZOL
;
; SLIDE RIGHT
;
RSLRIGHT JSR SPRKC
LDY #2
R033 LDA ($FD),Y
ROR A
DEY
DEY
LDX #3
R034 LDA ($FD),Y
ROR A
STA ($FD),Y
INY
DEX
BNE R034
INY
INY
CPY #$41
BNE R033
RTS
SLRIGHT LDY SN
LDA CMODTAB,Y
BEQ R048
JSR RSLRIGHT
R048 JSR RSLRIGHT
JMP RAJZOL
;
; SLIDE LEFT
;
RSLLEFT JSR SPRKC
LDY #0
R035 LDA ($FD),Y
ROL A
INY
INY
LDX #3
R036 LDA ($FD),Y
ROL A
STA ($FD),Y
DEY
DEX
BNE R036
INY
INY
INY
INY
CPY #$3F
BNE R035
RTS
SLLEFT LDY SN
LDA CMODTAB,Y
BEQ R047
JSR RSLLEFT
R047 JSR RSLLEFT
JMP RAJZOL
;
; FOKBEOSZTAS KI-BE
;
CHGRID LDA GRIDON
EOR #%00000001
STA GRIDON
JSR GRID
JMP LOOP
;
; MULTI-HIRES VALTAS
;
CHCOLMOD LDX SN
LDA CMODTAB,X
EOR #%00000001
STA CMODTAB,X
JMP UJGRID
;
; MIRROR HORIZ
;
MIRRORX JSR SPRKC
LDX #62
R037 LDY #2
R038 LDA ($FD),Y
STA SPRTMP,X
DEX
DEY
BPL R038
INC $FD
INC $FD
INC $FD
CPX #$FF
BNE R037
JSR COPYTMPS
LDA #0
STA UNDOEN
JMP RAJZOL
;
; TMP SPRITE VISSZAMASOLASA
;
COPTMPSC LDX #0
LDY SN
LDA CMODTMP
STA CMODTAB,Y
R080 LDA SZINTMP,X
STA BGRTAB,Y
INX
TYA
CLC
ADC #$40
TAY
BCC R080
COPYTMPS JSR SPRKC
LDY #62
R039 LDA SPRTMP,Y
STA ($FD),Y
DEY
BPL R039
RTS
;
; SPR SORSZAM NOVELES/CSOKKENTES
;
SNNOV INC SN
INC SN
SNCSOK DEC SN
LDA SN
AND #%00111111
STA SN
LDA #0
STA UNDOEN
JMP UJSPR
;
; SPR TORLESE
;
SPRERASE LDA #<SURETEXT
LDY #>SURETEXT
JSR YNKERDES
BNE R041
JSR COPSCTMP
LDY #62
LDA #0
R040 STA ($FD),Y
DEY
BPL R040
LDA #1
STA UNDOEN
R041 JMP RAJZOL
;
; KURZOR HOME
;
CRSRHOME JSR HOME
JMP RAJZOL
;
; SZINEK ALLITASA
;
SETCTEXT .ASC "SELECT:ê      ü  ú      û  "
.ASC "Å  ï  ñ  ó  ò  ô  ö  õ  í"
.BYTE 0
SETCOL SEC
SBC #"!"
STA AKTSZIN
R046 LDA #<SETCTEXT
LDY #>SETCTEXT
JSR PRINT
LDX AKTSZIN
LDA COLTABLO,X
CLC
ADC SN
STA TMP3+1
TAX
LDA BGRTAB,X
ASL A
CLC
ADC #7
TAY
LDA #209
STA SCRRAM,Y
R042 JSR GETIN
CMP #0
BEQ R042
LDX TMP3+1
CMP #"+"
BEQ R043
CMP #"-"
BEQ R044
LDA #<TEXTCLR
LDY #>TEXTCLR
JSR PRINT
JSR JELAKTSZ
JMP LOOP
R043 DEC BGRTAB,X
JMP R045
R044 INC BGRTAB,X
R045 LDA BGRTAB,X
AND #%00001111
STA BGRTAB,X
JSR COLPRINT
JSR DATA
JMP R046
;
; HELP
;
HELP LDA #0
STA VIC+21
LDA #<HLP1TEXT
LDY #>HLP1TEXT
JSR PRINT
R049 JSR GETIN
CMP #0
BEQ R049
CMP #"H"
BNE R050
LDA #<HLP2TEXT
LDY #>HLP2TEXT
JSR PRINT
R051 JSR GETIN
CMP #0
BEQ R051
CMP #"H"
BEQ HELP
R050 PHA
LDA #%00000101
STA VIC+21
LDA #<SCRTEXT
LDY #>SCRTEXT
JSR PRINT
JSR GRID
JSR OBJPRINT
JSR SNPRINT
JSR DATA
JSR JELAKTSZ
JSR POZALL
PLA
CMP #" "
BEQ R105
JMP BILLELL
R105 JMP CRSR
;
; POZICIO MUNKATERULET BEALLITAS
;
POZALL LDA REGIPOZ
STA $FB
LDA REGIPOZ+1
STA $FC
LDA REGIBAJT
STA UJBAJT
LDA REGIBIT
STA UJBIT
RTS
;
; INVERT
;
INVERT JSR SPRKC
LDY #62
R052 LDA ($FD),Y
EOR #%11111111
STA ($FD),Y
DEY
BPL R052
JMP RAJZOL
;
; MIRROR BYTES - HIRES
;
MIRBH JSR SPRKC
LDY #62
R053 LDA #7
STA TMP
LDA ($FD),Y
R054 ASL A
PHA
TXA
ROR A
TAX
PLA
DEC TMP
BPL R054
TXA
STA SPRTMP,Y
DEY
BPL R053
RTS
;
; MIRROR BYTES - MULTI
;
MIRBM JSR SPRKC
LDY #62
R055 LDA #3
STA TMP
LDA ($FD),Y
R056 ASL A
PHP
ASL A
STA TMP+1
TXA
ROR A
PLP
ROR A
TAX
LDA TMP+1
DEC TMP
BPL R056
TXA
STA SPRTMP,Y
DEY
BPL R055
RTS
;
; ROTATE RUTIN
;
ROTATER LDX SN
LDA CMODTAB,X
BNE R057
JSR MIRBH
JMP R058
R057 JSR MIRBM
R058 LDY #62
LDX #0
R059 LDA SPRTMP,X
STA ($FD),Y
INX
DEY
BPL R059
LDA #0
STA UNDOEN
RTS
ROTATE JSR ROTATER
JMP RAJZOL
;
; MIRROR VERT
;
MIRRORY JSR ROTATER
JMP MIRRORX
;
; CRSR UP
;
UP SEC
LDA REGIBAJT
SBC #3
STA UJBAJT
SEC
LDA $FB
SBC #40
STA $FB
BCS R060
DEC $FC
R060 JMP ELLPOZ
;
; CRSR DOWN
;
DOWN CLC
LDA REGIBAJT
ADC #3
STA UJBAJT
CLC
LDA $FB
ADC #40
STA $FB
BCC R061
INC $FC
R061 JMP ELLPOZ
;
; POZICIO ELLENORZES
;
ELLPOZ LDY #0
LDA ($FB),Y
CMP #128
BCS R062
JMP LOOP
R062 LDA $FB
STA REGIPOZ
LDA $FC
STA REGIPOZ+1
LDA UJBAJT
STA REGIBAJT
LDA UJBIT
STA REGIBIT
JMP CRSR
;
; CRSR LEFT
;
LEFT LDX SN
LDA CMODTAB,X
BEQ R063
JSR LEFTR
R063 JSR LEFTR
JMP ELLPOZ
LEFTR INC UJBIT
LDA UJBIT
CMP #8
BNE R064
LDA #0
STA UJBIT
DEC UJBAJT
R064 DEC $FB
LDA $FB
CMP #$FF
BNE R065
DEC $FC
R065 RTS
;
; CRSR RIGHT
;
RIGHT LDX SN
LDA CMODTAB,X
BEQ R066
JSR RIGHTR
R066 JSR RIGHTR
JMP ELLPOZ
RIGHTR DEC UJBIT
BPL R067
LDA #7
STA UJBIT
INC UJBAJT
R067 INC $FB
BNE R068
INC $FC
R068 RTS
;
; SPRITE MASOLASA SPRTMP-BE
;
COPSCTMP LDX #3
R079 LDA AKTBGR,X
STA SZINTMP,X
DEX
BPL R079
LDY SN
LDA CMODTAB,Y
STA CMODTMP
COPYSTMP JSR SPRKC
LDY #62
R070 LDA ($FD),Y
STA SPRTMP,Y
DEY
BPL R070
RTS
;
; UNDO
;
UNDO LDA UNDOEN
BEQ R071
JSR COPTMPSC
JMP UJGRID
R071 JMP CRSR
;
; PONT BESZINEZESE
;
SETDOT SEC
SBC #"1"
STA AKTSZIN
JSR JELAKTSZ
SPACE JSR SPRKC
LDX SN
LDY CMODTAB,X
BEQ SETH
LDA REGIBIT
LSR A
TAX
LDY REGIBAJT
LDA ($FD),Y
AND CLRMDOT,X
STA ($FD),Y
LDA AKTSZIN
R072 DEX
BMI R073
ASL A
ASL A
JMP R072
R073 ORA ($FD),Y
STA ($FD),Y
JMP RAJZOL
CLRMDOT .BYTE %11111100,%11110011
.BYTE %11001111,%00111111
SETH LDX REGIBIT
LDY REGIBAJT
LDA AKTSZIN
LSR A
BNE R074
LDA ($FD),Y
AND CLRHDOT,X
JMP R075
R074 LDA ($FD),Y
ORA SETHDOT,X
R075 STA ($FD),Y
JMP RAJZOL
CLRHDOT .BYTE %11111110,%11111101
.BYTE %11111011,%11110111
.BYTE %11101111,%11011111
.BYTE %10111111,%01111111
SETHDOT .BYTE %00000001,%00000010
.BYTE %00000100,%00001000
.BYTE %00010000,%00100000
.BYTE %01000000,%10000000
;
; AKTUALIS SZIN KIJELZESE
;
JELAKTSZ LDX #15
LDY #0
CLC
JSR PLOT
LDA #<TORL1234
LDY #>TORL1234
JSR PRINT
LDA AKTSZIN
ASL A
TAX
LDA POZ1234,X
STA $FD
LDA POZ1234+1,X
STA $FE
LDY #0
LDA ($FD),Y
EOR #%10000000
STA ($FD),Y
RTS
TORL1234 .ASC "1ù2ù3ù4"
.BYTE 0
POZ1234 .WORD 1624,1664,1704,1744
;
; COPY ES MERGE - CEL KIVALASZTAS
;
COPMERGR JSR PRINT
LDA SN
STA TMP3
LDA #1
STA UNDOEN
LDA #<COPYTEXT
LDY #>COPYTEXT
JSR PRINT
R078 JSR GETIN
CMP #13
BEQ R076
CMP #"-"
BEQ R077
CMP #"+"
BNE R078
INC SN
INC SN
R077 DEC SN
LDA SN
AND #%00111111
STA SN
JSR GRID
JSR SNPRINT
JSR DATA
JMP R078
R076 LDA #<TEXTCLR
LDY #>TEXTCLR
JSR PRINT
LDA #<CLCPTEXT
LDY #>CLCPTEXT
JSR PRINT
JSR COPSCTMP
LDA SN
PHA
LDA TMP3
STA SN
JSR SPRKC
LDA $FD
STA $A3
LDA $FE
STA $A4
PLA
STA SN
JSR SPRKC
LDY SN
LDX TMP3
LDA CMODTAB,X
STA CMODTAB,Y
R083 LDA BGRTAB,X
STA BGRTAB,Y
TXA
CLC
ADC #$40
TAX
TYA
CLC
ADC #$40
TAY
BCC R083
RTS
COPYTEXT .ASC "SELECT DESTINATION SPRITE WITH + - RET"
.BYTE 0
CPTOTEXT .ASC " COPY TO"
.BYTE 0
MGTOTEXT .ASC "MERGE TO"
.BYTE 0
CLCPTEXT .ASC "        "
.BYTE 0
;
; COPY
;
COPY LDA #<CPTOTEXT
LDY #>CPTOTEXT
JSR COPMERGR
LDY #62
R081 LDA ($A3),Y
STA ($FD),Y
DEY
BPL R081
JMP UJGRID
;
; COPY WITH MERGE
;
CMERGE LDA #<MGTOTEXT
LDY #>MGTOTEXT
JSR COPMERGR
LDY #62
R082 LDA ($A3),Y
ORA ($FD),Y
STA ($FD),Y
DEY
BPL R082
JMP UJGRID
FILNTEXT .ASC "ENTER FILENAME: "
.BYTE 0
DTTEXT .ASC "DISK OR TAPE? (D/T)      (STOP TO QUIT)"
.BYTE 0
FILENAME .ASC "           .SPR"
FILNTMP .ASC "          "
;
; FILENEV,EGYSEGSZAM MEGADASA
;
FILNAM LDA #0
STA UNDOEN
LDA #<FILNTEXT
LDY #>FILNTEXT
JSR PRINT
LDX #9
LDA #32
R084 STA FILENAME,X
DEX
BPL R084
LDA #0
STA 204
STA TMP2
R085 JSR GETIN
LDY TMP2
CMP #"à"
BEQ R123
CMP #13
BEQ R086
CMP #20
BEQ R087
CMP #32
BCC R085
CMP #96
BCS R085
CPY #10
BEQ R085
INC TMP2
STA FILENAME,Y
JSR CHROUT
JMP R085
R087 CPY #0
BEQ R085
JSR CHROUT
DEC TMP2
DEY
LDA #32
STA FILENAME,Y
JMP R085
R123 LDY #9
R124 LDA FILNTMP,Y
STA FILENAME,Y
DEY
BPL R124
R086 LDA #1
STA 204
LDY #9
R125 LDA FILENAME,Y
STA FILNTMP,Y
DEY
BPL R125
LDA #<DTTEXT
LDY #>DTTEXT
JSR PRINT
R088 JSR STOP
BNE R089
LDA #<TEXTCLR
LDY #>TEXTCLR
JSR PRINT
JMP LOOP
R089 JSR GETIN
LDX #1
CMP #"T"
BEQ R090
CMP #"D"
BNE R088
LDX #8
R090 STX TMP2+1
LDA #"ì"
JSR CHROUT
LDA #0
STA VIC+21
LDA #1
LDX TMP2+1
LDY #0
JSR SETLFS
LDA #15
LDX #<FILENAME
LDY #>FILENAME
JSR SETNAM
RTS
;
; VERIFY
;
VERIFYR JSR FILNAM
LDA #1
LDX #$80
LDY #$2E
JSR LOAD
BCS R097
JSR READST
AND #%00010000
BEQ R107
LDA #<VERRTEXT
LDY #>VERRTEXT
JMP R098
;
; LOAD
;
LOADR JSR FILNAM
LDA #0
LDX #$80
LDY #$2E
JSR LOAD
R092 BCS R097
JSR READST
AND #%10111111
BEQ R093
R100 LDA #<LERRTEXT
LDY #>LERRTEXT
JMP R101
R097 CMP #0
BNE R094
LDA #<STOPTEXT
LDY #>STOPTEXT
JMP R098
R094 CMP #4
BNE R099
LDA #<NOTFTEXT
LDY #>NOTFTEXT
JMP R098
R099 LDA #<DRIVTEXT
LDY #>DRIVTEXT
R098 JSR PRINT
R107 LDA #<PRESTEXT
LDY #>PRESTEXT
R101 JSR PRINT
LDA #0
STA NDX
R091 JSR GETIN
CMP #0
BEQ R091
R093 LDA #0
STA NDX
JMP SPRON
VERRTEXT .BYTE 13
.ASC "VERIFY ERROR"
.BYTE 13
.ASC "PLEASE TRY TO SAVE IT AGAIN!"
.BYTE 0
STOPTEXT .BYTE 13
.ASC "OPERATION TERMINATED BY THE STOP KEY"
.BYTE 0
NOTFTEXT .BYTE 13
.ASC "FILE NOT FOUND"
.BYTE 0
DRIVTEXT .BYTE 13
.ASC "DRIVE NOT PRESENT"
.BYTE 0
LERRTEXT .BYTE 13
.ASC "LOAD ERROR"
PRESTEXT .BYTE 13
.ASC "PRESS ANY KEY"
.BYTE 0
;
; SAVE
;
SAVER JSR FILNAM
LDA #$80
STA $FD
LDA #$2E
STA $FE
LDX #0
LDY #$40
LDA #$FD
JSR SAVE
BCC R104
R102 JMP R097
R103 JMP R100
;
; LOAD WITH MERGE
;
LMERGE JSR FILNAM
LDA #0
LDX #$80
LDY #$AE
JSR LOAD
BCS R102
JSR READST
AND #%10111111
BNE R103
LDA 1
AND #%11111110
STA 1
LDA #$2E
STA $A4
LDA #$AE
STA $FE
LDA #0
STA $A3
STA $FD
LDY #$80
LDX #2
R095 LDA ($FD),Y
STA ($A3),Y
INY
BNE R095
INC $A4
INC $FE
DEX
BNE R095
LDX #$10
R096 LDA ($FD),Y
ORA ($A3),Y
STA ($A3),Y
INY
BNE R096
INC $A4
INC $FE
DEX
BNE R096
LDA 1
ORA #%00000001
STA 1
R104 JMP SPRON
;
; OBJ NOVELES / CSOKKENTES
;
OBJNOV INC OBJ
INC OBJ
OBJCSOK DEC OBJ
LDA OBJ
AND #%00001111
STA OBJ
JSR OBJPRINT
JMP CRSR
;
; OBJECT KIIRAS
;
OBJPRINT LDX #20
LDY #9
CLC
JSR PLOT
LDX OBJ
JSR XPRINT
LDX #21
LDY #8
CLC
JSR PLOT
LDY OBJ
LDA FSTTAB,Y
AND #%00111111
STA FSTTAB,Y
TAX
JSR XPRINT
LDA #""
JSR CHROUT
LDY OBJ
LDA LSTTAB,Y
AND #%00111111
STA LSTTAB,Y
TAX
JSR XPRINT
RTS
;
; FIRST / LAST STEP
;
FSTEP LDA SN
LDY OBJ
STA FSTTAB,Y
R106 JSR OBJPRINT
JMP CRSR
LSTEP LDA SN
LDY OBJ
STA LSTTAB,Y
JMP R106
;
; ANIMALAS
;
ANIMATE LDY OBJ
LDX FSTTAB,Y
STX TMP3
LDA BGRTAB,X
STA VIC+33
CMP #1
BNE R110
LDA #0
STA COLOR
R110 LDA #<ANIMTEXT
LDY #>ANIMTEXT
JSR PRINT
LDA #160
STA VIC+2  ; OBJ X
LDA #140
STA VIC+3  ; OBJ Y
LDA #0
STA VIC+16 ; X MSB
LDA #%00000010
STA VIC+21 ; OBJ BEKAPCSOLASA
R115 LDX #0
LDY #16
CLC
JSR PLOT
LDY OBJ
LDX ASPTAB,Y
STX TMP2+1
JSR XPRINT
LDX #1
LDY #13
CLC
JSR PLOT
LDY OBJ
LDX OSPTAB,Y
STX TMP3+1
JSR XPRINT
R113 LDX TMP3
LDA MC1TAB,X
STA VIC+37
LDA MC2TAB,X
STA VIC+38
LDA SPRTAB,X
STA VIC+40
LDA CMODTAB,X
ASL A
STA VIC+28
TXA
CLC
ADC #192
STA SPRMUT+1
LDY OBJ
TXA
CMP LSTTAB,Y
BNE R111
LDA FSTTAB,Y
STA TMP3
JMP R112
R111 INC TMP3
LDA TMP3
AND #%00111111
STA TMP3
R112 JSR STOP
BNE R109
LDA #11
STA VIC+33
LDA #1
STA COLOR
JMP SPRON
R109 INC TMP2
BNE R109
LDY OBJ
DEC TMP2+1
BNE R126
LDA ASPTAB,Y
STA TMP2+1
JMP R113
R126 DEC TMP3+1
BNE R112
LDA OSPTAB,Y
STA TMP3+1
R114 JSR GETIN
LDX OBJ
CMP #"ë"
BEQ AUP
CMP #""
BEQ ADOWN
CMP #"ù"
BEQ ALEFT
CMP #""
BEQ ARIGHT
CMP #"€"
BEQ OSPNOV
CMP #"›"
BEQ OSPCSOK
PHA
TXA
ORA #%00010000
TAX
PLA
CMP #"-"
BEQ OSPCSOK
CMP #"+"
BNE R112
OSPNOV INC OSPTAB,X
LDA OSPTAB,X
CMP #100
BCC R118
LDA #2
R119 STA OSPTAB,X
R118 JMP R115
OSPCSOK DEC OSPTAB,X
LDA OSPTAB,X
CMP #1
BNE R118
LDA #99
JMP R119
AUP DEC VIC+3
JMP R112
ADOWN INC VIC+3
JMP R112
ALEFT DEC VIC+2
LDA VIC+2
CMP #$FF
R116 BNE R117
LDA VIC+16
EOR #%00000010
STA VIC+16
R117 JMP R112
ARIGHT INC VIC+2
JMP R116
ANIMTEXT .ASC "ìANIMATION SPEED:       ( +- TO CHANGE )"
.BYTE 13
.ASC "OBJECT SPEED:    ( SHIFT +- TO CHANGE ) "
.ASC "CRSR†KEYS TO MOVE OBJECT, STOP TO QUIT"
.BYTE 0
;
; 2.RESZ SZOVEGEI
;
SCRTEXT  = $1750 ; SZERKESZTO
QUITTEXT = $18F1 ; KILEPES
HLP1TEXT = $1D7B ; HELP SCREEN 1
HLP2TEXT = $1B1F ; HELP SCREEN 2
